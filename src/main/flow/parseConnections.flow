version 0.1

flow ParseConnections {
  [ParserData]-> connections(ParseMultiple1) -> ;
  connections() subOut [ParserData]-> chain(ParseAll) -> subIn connections() ;
  chain() semOut [ParserData]-> semantic(SemanticCreateChain) -> semIn chain() ;

  chain() subOut.0 [ParserData]-> chainBeg(ConnectionChainBegin) -> subIn chain() ;
  chain() subOut.1 [ParserData]-> chainMids(ParseMultiple0) -> subIn chain() ;
  chain() subOut.2 [ParserData]-> optChainEnd(ParseOptional) -> subIn chain() ;
  chain() subOut.3 [ParserData]-> stmtEnd(ParseStatementEnd) -> subIn chain() ;

  chainMids() subOut [ParserData]-> chainMid(ParseChainMiddle) -> subIn chainMids() ;

  optChainEnd() subOut [ParserData]-> chainEnd(ParseChainEnd) -> subIn optChainEnd() ;
}

flow ParseChainBegin {
  [ParserData]-> chainBeg(ParseAlternatives) -> ;

  chainBeg() subOut.0 [ParserData]-> chainBegMax(ParseAll) -> subIn chainBeg() ;
  chainBeg() subOut.1 [ParserData]-> chainBegMin(ParseAll) -> subIn chainBeg() ;

  chainBegMax() semOut [ParserData]-> maxSemantic(SemanticCreateChainBegMax) -> semIn chainBegMax() ;
  chainBegMax() subOut.0 [ParserData]-> optPortMax(ParseOptPort) -> subIn chainBegMax() ;
  chainBegMax() subOut.1 [ParserData]-> chainMid(ParseChainMiddle) -> subIn chainBegMax() ;

  chainBegMin() semOut [ParserData]-> minSemantic(SemanticCreateChainBegMin) -> semIn chainBegMin() ;
  chainBegMin() subOut.0 [ParserData]-> opNameParens(ParseOperationNameParens) -> subIn chainBegMin() ;
  chainBegMin() subOut.1 [ParserData]-> optPortMin(ParseOptPort) -> subIn chainBegMin() ;
}

flow ParseChainMiddle {
  [ParserData]-> chainMid(ParseAll) -> ;
  chainMid() semOut [ParserData]-> semantic(SemanticCreateChainMiddle) -> semIn chainMid() ;

  chainMid() subOut.0 [ParserData]-> arrow(ParseArrow) -> subIn chainMid() ;
  chainMid() subOut.1 [ParserData]-> connPart(ParseConnectionPart) -> subIn chainMid() ;
}

flow ParseChainEnd {
  [ParserData]-> chainEnd(ParseAll) -> ;
  chainEnd() semOut [ParserData]-> semantic(SemanticCreateChainEnd) -> semIn chainEnd() ;

  chainEnd() subOut.0 [ParserData]-> arrow(ParseArrow) -> subIn chainEnd() ;
  chainEnd() subOut.1 [ParserData]-> optPort(ParseOptPort) -> subIn chainEnd() ;
}
