version 0.1

flow ParseOptOperationType {
  [ParserData]-> optOpType(ParseOptional) -> ;
  optOpType() subOut [ParserData]-> opType(ParseAll) -> subIn optOpType() ;
  opType() semOut [ParserData]-> (SemanticCreateOperationType) -> semIn opType() ;

  opType() subOut.0 [ParserData]-> (ParseBigIdent) -> subIn opType() ;
  opType() subOut.1 [ParserData]-> (ParseOptSpc) -> subIn opType() ;
}

flow ParseArrow {
  [ParserData]-> arrow(ParseAll) -> ;
  arrow() semOut [ParserData]-> (SemanticCreateArrow) -> semIn arrow() ;

  arrow() subOut.0 [ParserData]-> spcCom1(ParseSpaceComment) -> subIn arrow() ;
  arrow() subOut.1 [ParserData]-> optType(ParseOptional) -> subIn arrow() ;
  // config: literal = "->"
  arrow() subOut.2 [ParserData]-> litArr(ParseLiteral) -> subIn arrow() ;
  arrow() subOut.3 [ParserData]-> spcCom2(ParseSpaceComment) -> subIn arrow() ;

  optType() subOut [ParserData]-> type(ParseAll) -> subIn optType() ;

  // config: literal = "["
  type() subOut.0 [ParserData]-> openType(ParseLiteral) -> subIn type() ;
  type() subOut.1 [ParserData]-> spc1(ParseOptSpc) -> subIn type() ;
  type() subOut.2 [ParserData]-> typeName(ParseBigIdent) -> subIn type() ;
  type() subOut.3 [ParserData]-> spc2(ParseOptSpc) -> subIn type() ;
  // config: literal = "]"
  type() subOut.4 [ParserData]-> closeType(ParseLiteral) -> subIn type() ;
}

flow ParseOptPortSpc {
  [ParserData]-> optPortSpc(ParseOptional) -> ;
  optPortSpc() subOut [ParserData]-> portSpc(ParseAll) -> subIn optPortSpc() ;

  portSpc() semOut [ParserData]-> (SemanticCreatePortSpc) -> semIn portSpc() ;

  portSpc() subOut.0 [ParserData]-> pport(ParsePort) -> subIn portSpc() ;
  // config: acceptNewLine = false
  portSpc() subOut.1 [ParserData]-> space(ParseSpace) -> subIn portSpc() ;
}

flow ParseOptPort {
  [ParserData]-> optPort(ParseOptional) -> ;
  optPort() subOut [ParserData]-> pport(ParsePort) -> subIn optPort() ;
}

flow ParsePort {
  [ParserData]-> port(ParseAll) -> ;
  port() semOut [ParserData]-> semantic(SemanticCreatePort) -> semIn port() ;

  port() subOut.0 [ParserData]-> portName(ParseSmallIdent) -> subIn port() ;
  port() subOut.1 [ParserData]-> optPortNum(ParseOptional) -> subIn port() ;

  optPortNum() subOut [ParserData]-> portNum(ParseAll) -> subIn optPortNum() ;
  portNum() semOut [ParserData]-> (SemanticCreatePortNum) -> semIn portNum() ;

  // config: literal = "."
  portNum() subOut.0 [ParserData]-> dot(ParseLiteral) -> subIn portNum() ;
  portNum() subOut.1 [ParserData]-> num(ParseNatural) -> subIn portNum() ;
}
